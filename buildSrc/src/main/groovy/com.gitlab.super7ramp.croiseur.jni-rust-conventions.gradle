/*
 * SPDX-FileCopyrightText: 2023 Antoine Belvire
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

/**
 * Conventions for Java libraries relying on JNI implemented in Rust.
 */

plugins {
    id("com.gitlab.super7ramp.croiseur.java-library-conventions")
}

// Configuration of the rust native library
configurations.register("rust") {
    canBeConsumed = false
    canBeResolved = true
}

tasks.withType(ProcessResources).named("processResources").configure {
    // Copy the native library into the final jar
    from(configurations.named("rust"))
}

tasks.withType(Jar).named("jar").configure {
    /*
     * As jar includes a native library, it is suffixed with the native library operating system
     * identification. It is assumed that native library is not cross-compiled, hence using current
     * system information.
     */
    archiveClassifier.set(currentOperatingSystemIdentifier())
}

tasks.withType(Test).configureEach {
    // Some settings to ease native code debugging
    environment("RUST_BACKTRACE", "1")
    jvmArgs("-Xcheck:jni")
}

static def currentOperatingSystemIdentifier() {
    def osName = System.getProperty("os.name").toLowerCase(Locale.ENGLISH)
    def osArch = System.getProperty("os.arch").toLowerCase(Locale.ENGLISH)
    "${osName}-${osArch}"
}