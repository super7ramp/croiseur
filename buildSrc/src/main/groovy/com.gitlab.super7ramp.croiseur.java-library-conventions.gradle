/*
 * SPDX-FileCopyrightText: 2023 Antoine Belvire
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

/**
 * Conventions for Java libraries.
 */

plugins {
    id 'com.gitlab.super7ramp.croiseur.java-conventions'
    id 'java-library'
    id 'maven-publish'
}

java {
    withSourcesJar()
}

publishing {
    publications {
        "${camelCasedProjectName()}"(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitlabMaven'
            url = 'https://gitlab.com/api/v4/projects/43029946/packages/maven'
            credentials(HttpHeaderCredentials.class) {
                name = findProperty('croiseur.maven.token.name') ?: '<none_found>'
                value = findProperty('croiseur.maven.token.value') ?: '<none_found>'
            }
            authentication {
                header(HttpHeaderAuthentication.class)
            }
        }
    }
}

def camelCasedProjectName() {
    def dashLetterMatcher = project.name =~ ~/-([a-z])/
    dashLetterMatcher.replaceAll(match -> match.group(1).toUpperCase())
}