/*
 * SPDX-FileCopyrightText: 2023 Antoine Belvire
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

/**
 * Conventions for Java libraries.
 */

plugins {
    id 'com.gitlab.super7ramp.croiseur.java-conventions'
    id 'java-library'
    id 'maven-publish'
}

publishing {
    publications {
        "${camelCasedProjectName()}"(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitlabMaven'
            url = 'https://gitlab.com/api/v4/projects/43029946/packages/maven'
            credentials {
                username = findProperty('croiseur.maven.username') ?: '<none>'
                password = findProperty('croiseur.maven.password') ?: '<none>'
            }
        }
    }
}

def camelCasedProjectName() {
    def dashLetterMatcher = project.name =~ ~/-([a-z])/
    dashLetterMatcher.replaceAll(match -> match.group(1).toUpperCase())
}