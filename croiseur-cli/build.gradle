/*
 * SPDX-FileCopyrightText: 2023 Antoine Belvire
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

plugins {
    id 'com.gitlab.super7ramp.croiseur.java-cli-conventions'
}

dependencies {
    implementation project(':croiseur')
    runtimeOnly project(':croiseur-dictionary:croiseur-dictionary-hunspell-plugin')
    runtimeOnly project(':croiseur-dictionary:croiseur-dictionary-txt-plugin')
    runtimeOnly project(':croiseur-dictionary:croiseur-dictionary-xml-plugin')
    runtimeOnly project(':croiseur-solver:croiseur-solver-ginsberg-plugin')
    runtimeOnly project(':croiseur-solver:croiseur-solver-paulgb-plugin')
    dictionaryPath project(':croiseur-dictionary:croiseur-dictionary-txt-data')
    dictionaryPath project(':croiseur-dictionary:croiseur-dictionary-xml-data')
    // Don't pull hunspell dictionaries, since they are slower to read and are basically the same
    // as XML dictionaries
}

/**
 * Native build specifics.
 * <p>
 * This is used by picocli-codegen to generates the GraalVM configuration under
 * META-INF/native-image/picocli-generated.
 */
compileJava {
    options.compilerArgs += ["-Aother.resource.bundles=l10n.SolverMessages,l10n.HelpMessages,l10n.DictionaryMessages"]
    options.compilerArgs += ["-Aother.resource.patterns=.*logging.properties,.*crossword_composer_jni.(dll|dylib|so)"]
}

/**
 * Other native build specifics.
 * <p>
 * Things that cannot be configured via picocli-codegen, e.g. JNI.
 */
graalvmNative {
    binaries {
        main {
            def projectId = "${project.group}.${project.name}".replace('-', '.')
            def jniConfPath = "${project.buildDir}/resources/main/META-INF/native-image/manual/${projectId}/jni-config.json"
            buildArgs.add("-H:JNIConfigurationFiles=${jniConfPath}")
        }
    }
}

/**
 * Configures parameters to be used when launch the application via './gradlew run'.*/
// TODO find a way to move this to application convention plugin
tasks.named('run') {
    def dictionaryPath = configurations.getByName('dictionaryPath').asPath
    jvmArgs.add('-Dcom.gitlab.super7ramp.croiseur.dictionary.path=' + dictionaryPath)
}
