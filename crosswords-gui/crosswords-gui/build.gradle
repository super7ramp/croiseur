/*
 * SPDX-FileCopyrightText: 2023 Antoine Belvire
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

plugins {
    id 'com.gitlab.super7ramp.crosswords.java-application-conventions'
    alias(sbom.plugins.javafx)
}

javafx {
    version = sbom.versions.java.get()
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation project(':crosswords')
    implementation project(':crosswords-gui:crosswords-gui-controller')
    implementation project(':crosswords-gui:crosswords-gui-presenter')
    implementation project(':crosswords-gui:crosswords-gui-view')
    implementation project(':crosswords-gui:crosswords-gui-view-model')
    runtimeOnly project(':crosswords-dictionary:crosswords-dictionary-hunspell-plugin')
    runtimeOnly project(':crosswords-dictionary:crosswords-dictionary-txt-plugin')
    runtimeOnly project(':crosswords-dictionary:crosswords-dictionary-xml-plugin')
    runtimeOnly project(':crosswords-solver:crosswords-solver-ginsberg-plugin')
    // Don't pull paulgb solver: There is no way to select the solver in GUI for now
    dictionaryPath project(':crosswords-dictionary:crosswords-dictionary-txt-data')
    dictionaryPath project(':crosswords-dictionary:crosswords-dictionary-xml-data')
    // Don't pull hunspell dictionaries, since they are slower to read and are basically the same
    // as XML dictionaries
}

/**
 * Configures parameters to be used when launch the application via './gradlew run'.
 */
// TODO find a way to move this to application convention plugin
tasks.named('run') {
    def dictionaryPath = configurations.getByName('dictionaryPath').asPath
    jvmArgs.add('-Dcom.gitlab.super7ramp.crosswords.dictionary.path=' + dictionaryPath)
}
