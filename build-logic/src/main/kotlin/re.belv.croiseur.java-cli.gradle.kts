/*
 * SPDX-FileCopyrightText: 2023 Antoine Belvire
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

/**
 * Conventions for Java CLI applications define:
 * <ul>
 *     <li>A systematic mainModule/mainClass format
 *     <li>A dependency on the picocli framework
 *     <li>A dependency on graalvm to allow native compilation
 * </ul>
 */

plugins {
    id("re.belv.croiseur.java-application")
    // Cannot use version catalog in plugins block of pre-compiled script plugins, i.e.
    // "alias(sbom.plugins.native)" doesn't work (gh#gradle/gradle#15383)
    id("org.graalvm.buildtools.native")
}

// Hack to make version catalog works with kotlin, see https://github.com/gradle/gradle/issues/15383
val sbom = the<org.gradle.accessors.dm.LibrariesForSbom>()
dependencies {
    implementation(sbom.picocli.framework)
    annotationProcessor(sbom.picocli.codegen)
}

/**
 * Uses picocli-codegen annotation processor to create native compilation configuration.
 */
tasks.withType(JavaCompile::class).configureEach {
    // The files generated by picocli-codegen are written to
    // META-INF/native-image/picocli-generated/${projectId}.
    val projectId = "${project.group}.${project.name}".replace('-', '.')
    options.compilerArgs.addAll(listOf("-Aproject=${projectId}", "-Averbose"))
}

/**
 * Native compilation option conventions.
 */
graalvmNative {
    binaries {
        named("main") {
            // All locales for which Croiseur has translations should be included
            buildArgs.add("-H:IncludeLocales=en,fr")
            buildArgs.add("-H:+AddAllCharsets")
            verbose = true
        }
        named("test") {
            buildArgs.add("-H:IncludeLocales=en,fr")
            buildArgs.add("-H:+AddAllCharsets")
            quickBuild = true
            verbose = true
        }
    }
}
