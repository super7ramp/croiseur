/*
 * SPDX-FileCopyrightText: 2023 Antoine Belvire
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

plugins {
    id 'com.gitlab.super7ramp.crosswords.java-cli-conventions'
}

dependencies {
    implementation project(':crosswords')
    runtimeOnly project(':crosswords-dictionary:crosswords-dictionary-hunspell-plugin')
    runtimeOnly project(':crosswords-dictionary:crosswords-dictionary-txt-plugin')
    runtimeOnly project(':crosswords-dictionary:crosswords-dictionary-xml-plugin')
    runtimeOnly project(':crosswords-solver:crosswords-solver-ginsberg-plugin')
    runtimeOnly project(':crosswords-solver:crosswords-solver-paulgb-plugin')
    dictionaryPath project(':crosswords-dictionary:crosswords-dictionary-txt-data')
    dictionaryPath project(':crosswords-dictionary:crosswords-dictionary-xml-data')
    // Don't pull hunspell dictionaries, since they are slower to read and are basically the same
    // as XML dictionaries
}

/**
 * Native build specifics.
 */
graalvmNative {
    binaries {
        main {
            // Dictionary module requires the JVM locales for its dictionary management (JVM
            // locales are not included by default in native build)
            buildArgs.add('-H:IncludeLocales=fr,en')
        }
    }
}
/**
 * Configures parameters to be used when launch the application via './gradlew run'.
 */
// TODO find a way to move this to application convention plugin
tasks.named('run') {
    def dictionaryPath = configurations.getByName('dictionaryPath').asPath
    jvmArgs.add('-Dcom.gitlab.super7ramp.crosswords.dictionary.path=' + dictionaryPath)
}
